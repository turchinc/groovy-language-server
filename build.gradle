plugins {
  id "com.gradleup.shadow" version "8.3.6"
  id "java"
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

test {
  useJUnitPlatform()
}

ext.mainClass = "net.prominic.groovyls.GroovyLanguageServer"

tasks.build.dependsOn tasks.shadowJar

shadowJar {
  manifest {
    attributes "Main-Class": "net.prominic.groovyls.GroovyLanguageServer"
  }
}

repositories  {
  mavenCentral()
  maven { url 'https://repo.gradle.org/gradle/libs-releases' }
}

def lsp4jVersion = "0.24.0"

dependencies {
  implementation "org.eclipse.lsp4j:org.eclipse.lsp4j:$lsp4jVersion"
  implementation "org.eclipse.lsp4j:org.eclipse.lsp4j.jsonrpc:$lsp4jVersion"
  implementation 'org.gradle:gradle-tooling-api:8.10.2'
  implementation "org.apache.groovy:groovy:4.0.26"
  implementation "com.google.code.gson:gson:2.13.1"
  implementation "io.github.classgraph:classgraph:4.8.179"
  implementation "org.slf4j:slf4j-api:2.0.17"
  implementation "org.slf4j:slf4j-simple:2.0.17"
  testImplementation "org.junit.jupiter:junit-jupiter-api:5.11.4"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.11.4"
}

// assuming that Moonshine-IDE is checked out in a folder that has the same
// parent folder as groovy-language-server, copies the built .jar file to the
// appropriate location
task deployMoonshine(type: Copy) {
    dependsOn(build)
    from "build/libs/groovy-language-server-all.jar"
    into "../Moonshine-IDE/ide/MoonshineSharedCore/src/elements/groovy-language-server/"
}
task deployMoonshineTest(type: Copy) {
    dependsOn(build)
    from "build/libs/groovy-language-server-all.jar"
    into "../Moonshine-IDE/ide/MoonshineDESKTOPevolved/bin-debug/elements/groovy-language-server/"
}
